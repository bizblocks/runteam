buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.jmix.gradle:jmix-gradle-plugin:2.6.1'
    }
}

group = 'dev.smartdata'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url 'https://global.repo.jmix.io/repository/public'
        }
    }

    jmix {
        bomVersion = '2.6.1'
        projectId = 'rt'
    }

    group = this.group
    version = this.version

    def props = new Properties()
    buildFile.withInputStream { props.load(it) }
    def subArchivesBaseName = props.getProperty('archivesBaseName')
    def archName = subArchivesBaseName.substring(1, subArchivesBaseName.length() - 1)

    java {
        withSourcesJar()
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri(System.getenv('GITHUB_REPOSITORY'))
                credentials {
                    username = rootProject['githubUser'] ?: System.getenv("GITHUB_USERNAME")
                    password = rootProject['githubToken'] ?: System.getenv("GITHUB_TOKEN")
                }
            }
                maven {
                name = "SmartDataGitlab"
                url = uri("https://git.smartdata.dev/api/v4/projects/418/packages/maven")
                credentials {
                    username = rootProject['gitlabUser']
                    password = rootProject['gitlabToken']
                }
            }
        }
        publications {
            javaMaven(MavenPublication) {
                artifactId = archName
                from components.java
            }
        }
    }

    // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
    tasks.withType(GenerateModuleMetadata) {
        enabled = true
    }

    project.tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-parameters")
    }
}
